{
  'vars' {
    'dateRange' [
      NOW 10 d -
      NOW
    ]
  }
  'options' {
    'scheme' 'NINETEEN_EIGHTY_FOUR'
    'customStyles' {
      '.discovery-tile'
      <'
          background-color: #3A3C4622;
          border-radius: 50px;
      '>

      '.discovery-dashboard-main'
      <'
          font-size       : 15px;
          line-height     : 1.52;
          background: #221F2D !important;
          color           : #FFFFFF;
          --wc-split-gutter-color            : #404040;
          --warp-view-pagination-bg-color    : #343a40 !important;
          --warp-view-pagination-border-color: #6c757d;
          --warp-view-datagrid-odd-bg-color  : rgba(255, 255, 255, .05);
          --warp-view-datagrid-odd-color     : #FFFFFF;
          --warp-view-datagrid-even-bg-color : #212529;
          --warp-view-datagrid-even-color    : #FFFFFF;
          --warp-view-font-color             : #FFFFFF;
          --warp-view-chart-label-color      : #FFFFFF;
          --gts-stack-font-color             : #FFFFFF;
          --warp-view-resize-handle-color    : #111111;
          --warp-view-chart-legend-bg        : #000;
          --gts-labelvalue-font-color        : #ccc;
          --gts-separator-font-color         : #FFFFFF;
          --gts-labelname-font-color         : rgb(105, 223, 184);
          --gts-classname-font-color         : rgb(126, 189, 245);
          --warp-view-chart-legend-color     : #FFFFFF;
          --wc-tab-header-color              : #FFFFFF;
          --wc-tab-header-selected-color     : #404040;
          --warp-view-input-bg-color         : #2e2b3f!important;
          --warp-view-input-label-color      : #FFFFFF!important;
          --warp-view-input-border-color     : #52525b!important;
      '>
      '.discovery-dashboard-main > h1, p'
      <'
          display: none !important;
      '>

      '.tabular-wrapper-inner'
      <'
          height: fit-content !important;
      '>
      '.input-date-range'
      <'
          height: 50px!important;
          min-height: 50px!important;
      '>
      '.discovery-dashboard-wrapper'
      <'
        grid-auto-rows: minmax(50px, auto)!important;
      '>
    }
  }
  'tiles' [
    {
      'type' 'input:date-range'
      'x' 0
      'y' 0
      'w' 12
      'h' 1
      'macro' <% {
        'data' $dateRange
        'events' [
          { 'tags' [ 'dateRange' ] 'type' 'variable' 'selector' 'dateRange' }
        ]
      } %>
    }
    {
      'title' ''
      'x' 0
      'y' 1
      'w' 12
      'h' 1
      'type' 'bar'
      'options' { 'eventHandler' 'type=(variable),tag=dateRange' }
      'macro' <%
        /* CONSTANTS TO DEFINE BEGIN SECTION */
        5 'nbDefaults' STORE
        'YOUR_PROJECT_ID' 'projectId' STORE
        /* CONSTANTS TO DEFINE END SECTION */

        '@TOKEN@' 'readToken' STORE
        $dateRange 0 GET 'startDate' STORE
        $dateRange 1 GET 'endDate' STORE

        <%
        SAVE 'context' STORE
        'gts' STORE
        $gts ATTRIBUTES 'attributs' STORE
        $attributs 'objectInstance_name' GET 'objectInstanceName' STORE
        $attributs 'attributeModel_name' GET 'attributeModelName' STORE

        [ $objectInstanceName $attributeModelName ] '-' JOIN 'gtsName' STORE
        $gts $endDate ATTICK 4 GET 'countValue' STORE
        [ $gtsName $countValue ] 'gtsCount' STORE
        $gtsCount
        $context RESTORE
        %> 'extractCountValue' STORE

        {
        'token' $readToken
        'class' 'scorpio'
        'labels' { 'project_id' $projectId  'attributeModel_attributeModelSubType' 'alarm' }
        'start' $startDate
        'end' $endDate
        }
        FINDSETS STACKTOLIST 1 GET 'scorpId' GET 'scorpIds' STORE

        $scorpIds '|' JOIN '~' SWAP + 'scorpIdRegex' STORE

        {
          'token' $readToken
          'class' 'scorpio'
          'labels' { 'project_id' $projectId  'scorpId' $scorpIdRegex }
          'start' $startDate
          'end' $endDate
        }
        FETCH 'inputs' STORE
        [ $inputs false mapper.ne 0 0 0 ] MAP
        [ SWAP bucketizer.count $endDate 0 1 ] BUCKETIZE 'buckitzedGts' STORE

        $buckitzedGts
        <% $extractCountValue EVAL %>

        FOREACH $buckitzedGts SIZE ->LIST <% 1 GET %> SORTBY REVERSE 0 $nbDefaults 1 - SUBLIST REVERSE 'rows' STORE

        {
          'title' 'Top 5 dÃ©fauts'
          'columns' [ 'Nombres' ]
          'rows' $rows
        } 'values' STORE
        $values
        {
          'data' $values
          'globalParams' {
            'bar' { 'horizontal' true }
            'hideYAxis' true
            'grid' {
              'containLabel' false
            }
            'unit' true
            'dataZoom' {
              'horizontal' {
                'zoomOnMouseWheel' false
              }
            }
            'tooltipPosition' 'right'
            'datasetNoAlpha' true
          }
        }
      %>
    }
  ]
}